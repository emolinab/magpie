*** |  (C) 2008-2025 Potsdam Institute for Climate Impact Research (PIK)
*** |  authors, and contributors see CITATION.cff file. This file is part
*** |  of MAgPIE and licensed under AGPL-3.0-or-later. Under Section 7 of
*** |  AGPL-3.0, you are granted additional permissions described in the
*** |  MAgPIE License Exception, version 1.0 (see LICENSE file).
*** |  Contact: magpie@pik-potsdam.de

i21_trade_bal_reduction(t_all,k_trade)=f21_trade_bal_reduction(t_all,"easytrade","%c21_trade_liberalization%");
i21_trade_bal_reduction(t_all,k_hardtrade21)=f21_trade_bal_reduction(t_all,"hardtrade","%c21_trade_liberalization%");


i21_trade_margin(i_ex,i_im,k_trade) = f21_trade_margin(i_ex,i_im,k_trade);

i21_trade_margin(i_ex,i_im,k_trade)$(i21_trade_margin(i_ex,i_im,k_trade) < 1e-6) = 5;


i21_import_supply_historical(i_ex,i_im,t_all,k_trade) = f21_import_supply_historical(i_ex,i_im,t_all,k_trade);



  loop(t_all,
     i21_trade_bilat_stddev(t_all,i_ex,i_im,k_trade)$(m_year(t_all) = sm_fix_SSP2 + 5) = f21_trade_bilat_stddev(i_ex,i_im,k_trade,"maxsd5");
     i21_trade_bilat_stddev(t_all,i_ex,i_im,k_trade)$(m_year(t_all) = sm_fix_SSP2 + 10) = f21_trade_bilat_stddev(i_ex,i_im,k_trade,"maxsd10");
    i21_trade_bilat_stddev(t_all,i_ex,i_im,k_trade)$(m_year(t_all)  >= sm_fix_SSP2 + 15) = f21_trade_bilat_stddev(i_ex,i_im,k_trade,"maxsd15");
  );



if ((s21_trade_tariff=1),
    i21_trade_tariff(t_all, i_ex,i_im,k_trade) = f21_trade_tariff(i_ex,i_im,k_trade);
  elseif (s21_trade_tariff=0),
     i21_trade_tariff(t_all, i_ex,i_im,k_trade) = 0;
 );

 if ((s21_trade_tariff_fadeout=1),
 loop(t_all,
    i21_trade_tariff(t_all,i_ex,i_im,k_trade)$(m_year(t_all) > s21_trade_tariff_startyear and m_year(t_all) < s21_trade_tariff_targetyear) = (1-((m_year(t_all)-s21_trade_tariff_startyear) /
                                                                                                                                            (s21_trade_tariff_targetyear-s21_trade_tariff_startyear))) * 
                                                                                                                                            i21_trade_tariff(t_all,i_ex,i_im,k_trade);
 i21_trade_tariff(t_all,i_ex,i_im,k_trade)$(m_year(t_all) <= s21_trade_tariff_startyear) = i21_trade_tariff(t_all,i_ex,i_im,k_trade); 
 i21_trade_tariff(t_all,i_ex,i_im,k_trade)$(m_year(t_all) >= s21_trade_tariff_targetyear) = 0 ; 
 );
 );

 loop(t_all,
    i21_trade_tariff(t_all,i_ex,i_im,k_trade)$(m_year(t_all) <= sm_fix_SSP2) =  i21_trade_tariff(t_all,i_ex,i_im,k_trade);
    i21_trade_tariff(t_all,i_ex,i_im,k_trade)$(m_year(t_all) > sm_fix_SSP2)=  i21_trade_tariff(t_all,i_ex,i_im,k_trade) * s21_tariff_factor;
  );


 loop(t_all,
    i21_stddev_lib_factor(t_all)$(m_year(t_all) <= sm_fix_SSP2) =  1;
    i21_stddev_lib_factor(t_all)$(m_year(t_all) > sm_fix_SSP2)=  s21_stddev_lib_factor;
   );

  i21_import_supply_scenario(t_all) = 1;

 m_linear_time_interpol(i21_import_supply_scenario,sm_fix_SSP2,s21_import_supply_scenario_targetyear,1,s21_import_supply_scenario);


i21_trade_margin(i_ex, i_im,"wood")$(i21_trade_margin(i_ex, i_im,"wood") < s21_min_trade_margin_forestry) = s21_min_trade_margin_forestry;
i21_trade_margin(i_ex, i_im,"woodfuel")$(i21_trade_margin(i_ex, i_im,"woodfuel") < s21_min_trade_margin_forestry) = s21_min_trade_margin_forestry;


*** Initialize bloc membership indicator (1 = same bloc, 0 = different bloc)
i21_bloc_trade_indicator(i_ex,i_im) = 0;
loop(trade_bloc21,
   i21_bloc_trade_indicator(i_ex,i_im)$(bloc_regions21(trade_bloc21,i_ex) and bloc_regions21(trade_bloc21,i_im)) = 1;
);

*** Calculate effective tariffs based on bloc membership
*** Intra-bloc: tariffs can be reduced via s21_intrabloc_tariff_factor
*** Cross-bloc: tariffs can be increased via s21_crossbloc_tariff_factor

i21_intrabloc_tariff(t_all,i_ex,i_im,k_trade) = i21_trade_tariff(t_all,i_ex,i_im,k_trade) * s21_intrabloc_tariff_factor;
i21_crossbloc_tariff(t_all,i_ex,i_im,k_trade) = i21_trade_tariff(t_all,i_ex,i_im,k_trade) * s21_crossbloc_tariff_factor;

*** Combined effective tariff: apply intra-bloc tariff if same bloc, cross-bloc if different
i21_bloc_tariff(t_all,i_ex,i_im,k_trade) =
    i21_bloc_trade_indicator(i_ex,i_im) * i21_intrabloc_tariff(t_all,i_ex,i_im,k_trade)
    + (1 - i21_bloc_trade_indicator(i_ex,i_im)) * i21_crossbloc_tariff(t_all,i_ex,i_im,k_trade);